---
title: Reversal of formal series
---

In this section we define right derivatives and reversal of formal series,
and discuss their basic properties.

```
{-# OPTIONS --guardedness --sized-types #-}

open import Preliminaries.Base renaming (_++_ to _++áµ¥_)
module General.Reversal
    (R : CommutativeRing)
    (Î£ : Set)
    where

open import Size
open import Preliminaries.Algebra R
open import Preliminaries.Vector 

open import General.Terms R
    renaming (_+_ to _[+]_; _*_ to _[*]_; _Â·_ to _[Â·]_)
    
open import General.Series R Î£
open import General.ProductRules R
open import General.Products R Î£

private variable
    i : Size
    m n k â„“ : â„•
    f g : A âŸª Î£ âŸ« i
```

# Right derivative

We begin by defining the *right derivative* of a formal series,
which is the operation symmetric to the left derivative.

```
Î´Ê³ : âˆ€ {j : Size< i} â†’ Î£ â†’ A âŸª Î£ âŸ« i â†’ A âŸª Î£ âŸ« j
Î½ (Î´Ê³ a f) = Î½ (Î´Ë¡ a f)
Î´ (Î´Ê³ a f) b = Î´Ê³ a (Î´Ë¡ b f)
```

The additional size parameters allow Agda to verify that the definition is productive.

We define the homomorphic extension `Î´Ê³*` of the right derivative to all finite words.

```
module _ where
    open import Preliminaries.List

      -- homomorphic extension to all words
    Î´Ê³* : Î£ * â†’ A âŸª Î£ âŸ« â†’ A âŸª Î£ âŸ«
    Î´Ê³* Îµ f = f
    Î´Ê³* (a âˆ· w) f = Î´Ê³* w (Î´Ê³ a f)
```

## Properties of right derivatives

Left and right derivatives commute by definition,
however it is useful to state this explicitly.

```
Î´Ë¡-Î´Ê³ : âˆ€ (f : A âŸª Î£ âŸ«) a b â†’ Î´Ë¡ a (Î´Ê³ b f) â‰ˆ Î´Ê³ b (Î´Ë¡ a f)
Î´Ë¡-Î´Ê³ f a b = â‰ˆ-refl
```

Right derivatives preserve series equivalence.

```
Î´Ê³-cong :
    âˆ€ a â†’
    f â‰ˆ[ i ] g â†’
    {j : Size< i} â†’
    --------------------
    Î´Ê³ a f â‰ˆ[ j ] Î´Ê³ a g

Î½-â‰ˆ (Î´Ê³-cong a fâ‰ˆg) = Î½-â‰ˆ (Î´-â‰ˆ fâ‰ˆg a)
Î´-â‰ˆ (Î´Ê³-cong a fâ‰ˆg) b = Î´Ê³-cong a (Î´-â‰ˆ fâ‰ˆg b)

Î´Ê³-inv : âˆ€ a â†’ â‰ˆ-Invariance (Î´Ê³ a)
Î´Ê³-inv a fâ‰ˆg = Î´Ê³-cong a fâ‰ˆg
```

We show that right derivatives preserve the vector space structure.

```
open Properties

Î´Ê³-end-ðŸ˜ : âˆ€ a â†’ Endomorphic-ðŸ˜ (Î´Ê³ a)
Î½-â‰ˆ (Î´Ê³-end-ðŸ˜ a) = R-refl
Î´-â‰ˆ (Î´Ê³-end-ðŸ˜ a) b = Î´Ê³-end-ðŸ˜ a

Î´Ê³-end-+ : âˆ€ a â†’ Endomorphic-+ (Î´Ê³ a)
Î½-â‰ˆ (Î´Ê³-end-+ a f g) = R-refl
Î´-â‰ˆ (Î´Ê³-end-+ a f g) b = Î´Ê³-end-+ _ _ _

Î´Ê³-end-Â· : âˆ€ a â†’ Endomorphic-Â· (Î´Ê³ a)
Î½-â‰ˆ (Î´Ê³-end-Â· a c f) = R-refl
Î´-â‰ˆ (Î´Ê³-end-Â· a c f) b = Î´Ê³-end-Â· _ _ _
```

We show how right derivatives interact with the coefficient extraction operation.

```
module _ where
    open Inductive

    Î´Ê³-coeff : âˆ€ a w f â†’ Î´Ê³ a f âŸ¨ w âŸ© â‰¡ f âŸ¨ w âˆ·Ê³ a âŸ©
    Î´Ê³-coeff a Îµ f = refl
    Î´Ê³-coeff a (b âˆ· w) f = Î´Ê³-coeff a w (Î´Ë¡ b f)

    -- analogous to coeff-Î´Ë¡* :
    -- âˆ€ (u v : List Î£) (f : A âŸª Î£ âŸ«) â†’ Î´Ë¡* u f âŸ¨ v âŸ© â‰¡ f âŸ¨ u ++ v âŸ©
    coeff-Î´Ê³* : âˆ€ u v f â†’ Î´Ê³* u f âŸ¨ v âŸ© â‰¡ f âŸ¨ v ++ reverse u âŸ©
    coeff-Î´Ê³* Îµ v f =
        begin
            Î´Ê³* Îµ f âŸ¨ v âŸ© â‰¡âŸ¨âŸ©
            f âŸ¨ v âŸ© â‰¡âŸ¨ cong (Î» w â†’ f âŸ¨ w âŸ©) (++-identityÊ³ v) âŸ¨
            f âŸ¨ v ++ Îµ âŸ© â‰¡âŸ¨âŸ©
            f âŸ¨ v ++ reverse Îµ âŸ©
        âˆŽ where open â‰¡-Eq
    coeff-Î´Ê³* (a âˆ· u) v f = 
        begin
            Î´Ê³* (a âˆ· u) f âŸ¨ v âŸ© â‰¡âŸ¨âŸ©
            Î´Ê³* u (Î´Ê³ a f) âŸ¨ v âŸ© â‰¡âŸ¨ coeff-Î´Ê³* u v _ âŸ©
            Î´Ê³ a f âŸ¨ v ++ reverse u âŸ© â‰¡âŸ¨ Î´Ê³-coeff a (v ++ reverse u) f âŸ©
            f âŸ¨ (v ++ reverse u) âˆ·Ê³ a âŸ© â‰¡âŸ¨ cong (Î» x â†’ f âŸ¨ x âŸ©) (++-assoc v (reverse u) _) âŸ©
            f âŸ¨ v ++ (reverse u âˆ·Ê³ a) âŸ© â‰¡âŸ¨ cong (Î» x â†’ f âŸ¨ v ++ x âŸ©) (unfold-reverse a u) âŸ¨
            f âŸ¨ v ++ reverse (a âˆ· u) âŸ©
        âˆŽ where open â‰¡-Eq

    Î´Ê³-Î´Ê³* : âˆ€ a w f â†’ Î´Ê³ a (Î´Ê³* w f) â‰¡ Î´Ê³* (w âˆ·Ê³ a) f  
    Î´Ê³-Î´Ê³* a Îµ f = refl
    Î´Ê³-Î´Ê³* a (b âˆ· w) f = Î´Ê³-Î´Ê³* a w (Î´Ê³ b f)

    Î´Ê³-Î´Ë¡* : âˆ€ f a w â†’ Î´Ê³ a (Î´Ë¡* w f) â‰ˆ Î´Ë¡* w (Î´Ê³ a f)
    Î´Ê³-Î´Ë¡* f a Îµ = â‰ˆ-refl
    Î´Ê³-Î´Ë¡* f a (_ âˆ· w) = Î´Ê³-Î´Ë¡* _ a w

    coeff-Î´Ë¡*-Î´Ê³* :
        âˆ€ u v f w â†’
        -------------------------------------------------
        Î´Ë¡* u (Î´Ê³* v f) âŸ¨ w âŸ© â‰¡ f âŸ¨ u ++ w ++ reverse v âŸ©
        
    coeff-Î´Ë¡*-Î´Ê³* u v f w =
        begin
            Î´Ë¡* u (Î´Ê³* v f) âŸ¨ w âŸ©
            â‰¡âŸ¨ coeff-Î´Ë¡* u w _ âŸ©
            Î´Ê³* v f âŸ¨ u ++ w âŸ©
            â‰¡âŸ¨ coeff-Î´Ê³* v (u ++ w) _ âŸ©
            f âŸ¨ (u ++ w) ++ reverse v âŸ©
            â‰¡âŸ¨ cong (Î» x â†’ f âŸ¨ x âŸ©) (++-assoc u w (reverse v)) âŸ©
            f âŸ¨ u ++ (w ++ reverse v) âŸ©
        âˆŽ where open â‰¡-Eq
```

# Reversal

We define the *reversal* of a formal series,
which intuitively means that the series reads the input words backwards.

```
rev : A âŸª Î£ âŸ« â†’ A âŸª Î£ âŸ«
Î½ (rev f) = Î½ f
Î´ (rev f) a = rev (Î´Ê³ a f)
```

## Properties of reversal

The following rule connecting reversal, left and right derivatives holds by definition,
however it is useful to state it explicitly.

```
rev-Î´Ê³ : âˆ€ (f : A âŸª Î£ âŸ«) a â†’ rev (Î´Ê³ a f) â‰ˆ Î´Ë¡ a (rev f)
rev-Î´Ê³ f a = â‰ˆ-refl
```

The following variation is also useful, and we need to prove it explicitly.

```
Î´Ê³-rev : âˆ€ (f : A âŸª Î£ âŸ«) a â†’ Î´Ê³ a (rev f) â‰ˆ[ i ] rev (Î´Ë¡ a f)
Î½-â‰ˆ (Î´Ê³-rev f a) = R-refl
Î´-â‰ˆ (Î´Ê³-rev f a) b = Î´Ê³-rev (Î´Ê³ b f) a
```

Reversal preserves series equivalence.

```
rev-cong :
    f â‰ˆ[ i ] g â†’
    ------------------
    rev f â‰ˆ[ i ] rev g

Î½-â‰ˆ (rev-cong fâ‰ˆg) = Î½-â‰ˆ fâ‰ˆg
Î´-â‰ˆ (rev-cong fâ‰ˆg) a = rev-cong (Î´Ê³-cong a fâ‰ˆg)
```

Reversal is an involution.

```
rev-rev :
    âˆ€ (f : A âŸª Î£ âŸ«) â†’
    --------------------
    rev (rev f) â‰ˆ[ i ] f

Î½-â‰ˆ (rev-rev f) = R-refl
Î´-â‰ˆ (rev-rev f) a = 
  begin
    Î´Ë¡ a (rev (rev f))
      â‰ˆâŸ¨âŸ©
    rev (Î´Ê³ a (rev f))
      â‰ˆâŸ¨ rev-cong (Î´Ê³-rev f a) âŸ©
    rev (rev (Î´Ë¡ a f))
      â‰ˆâŸ¨ rev-rev _ âŸ©
    Î´Ë¡ a f
  âˆŽ where open EqS
```

We can express right derivatives in terms of left derivatives and a double reversal.

```
Î´Ê³-rev-rev :
    âˆ€ (f : A âŸª Î£ âŸ«) a â†’
    --------------------------------
    Î´Ê³ a f â‰ˆ[ i ] rev (Î´Ë¡ a (rev f))

Î´Ê³-rev-rev f a =
    begin
        Î´Ê³ a f â‰ˆâŸ¨ rev-rev _ âŸ¨
        rev (rev (Î´Ê³ a f))
            â‰ˆâŸ¨ rev-cong (rev-Î´Ê³ _ _) âŸ©
        rev (Î´Ë¡ a (rev f))
    âˆŽ where open EqS
```

Reversal respects the vector space structure.

```
rev-end-ðŸ˜ : Endomorphic-ðŸ˜ rev
Î½-â‰ˆ rev-end-ðŸ˜ = R-refl
Î´-â‰ˆ rev-end-ðŸ˜ a =
    begin
        rev (Î´Ê³ a ðŸ˜) â‰ˆâŸ¨ rev-cong (Î´Ê³-end-ðŸ˜ _) âŸ©
        rev ðŸ˜ â‰ˆâŸ¨ rev-end-ðŸ˜ âŸ©
        ðŸ˜
    âˆŽ where open EqS
```

```
rev-end-+ : Endomorphic-+ rev
Î½-â‰ˆ (rev-end-+ f g) = R-refl
Î´-â‰ˆ (rev-end-+ f g) a =
    begin
        Î´Ë¡ a (rev (f + g))
            â‰ˆâŸ¨âŸ©
        rev (Î´Ê³ a (f + g))
            â‰ˆâŸ¨ rev-cong (Î´Ê³-end-+ _ _ _) âŸ©
        rev (Î´Ê³ a f + Î´Ê³ a g)
            â‰ˆâŸ¨ rev-end-+ (Î´Ê³ a f) (Î´Ê³ a g) âŸ©
        rev (Î´Ê³ a f) + rev (Î´Ê³ a g)
            â‰ˆâŸ¨âŸ©
        Î´Ë¡ a (rev f) + Î´Ë¡ a (rev g)
    âˆŽ where open EqS
```

```
rev-end-Â· : Endomorphic-Â· rev
Î½-â‰ˆ (rev-end-Â· c f) = R-refl
Î´-â‰ˆ (rev-end-Â· c f) a =
    begin
        Î´Ë¡ a (rev (c Â· f))
            â‰ˆâŸ¨âŸ©
        rev (Î´Ê³ a (c Â· f))
            â‰ˆâŸ¨ rev-cong (Î´Ê³-end-Â· _ _ _) âŸ©
        rev (c Â· (Î´Ê³ a f))
            â‰ˆâŸ¨ rev-end-Â· c (Î´Ê³ a f) âŸ©
        c Â· rev (Î´Ê³ a f)
            â‰ˆâŸ¨âŸ©
        Î´Ë¡ a (c Â· rev f)
    âˆŽ where open EqS
```
